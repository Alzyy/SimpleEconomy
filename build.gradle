
allprojects {
    group = 'it.alzy.simpleeconomy'
    version = '2.4'

    repositories {
        mavenCentral()
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
        maven { url = 'https://jitpack.io' }
        maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
        maven { url = 'https://repo.extendedclip.com/releases/' }
    }
}

subprojects {
    apply plugin: 'java'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        if (JavaVersion.current().isJava10Compatible()) {
            options.release.set(21)
        }
    }

    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset = 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }
}
